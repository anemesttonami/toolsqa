plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.12.0'
}

allure {
    report {
        version.set('2.34.1')
    }
    adapter {
        aspectjWeaver.set(true) // это чтобы работало @Step
        frameworks {
            junit5 {
                adapterVersion.set('2.29.1')
                enabled.set(true) // явно включает адаптер , рекомендуется указывать
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(
            'com.codeborne:selenide:6.19.1',
            'org.junit.jupiter:junit-jupiter:5.9.3')
    testImplementation('io.qameta.allure:allure-selenide:2.19.0')
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8' // без этой настройки могут возникать проблемы с кодировкой, если в коде есть кириллица
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

tasks.register('positive_tests', Test) { //регистрируем таску гредл для сборки тестов с тегом positive
    useJUnitPlatform {
        includeTags("positive")
    }
}

tasks.register('negative_tests', Test) { //регистрируем таску гредл для сборки тестов с тегом negative
    useJUnitPlatform {
        includeTags("negative")
    }
}

test {
    useJUnitPlatform()
}